apply plugin: 'com.android.application'

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        buildConfigField "String", "GIT_HASH", "\"${getGitHash()}\""
        applicationId "de.konstanz.schulen.suso"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "0.0.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        config {
            keyAlias "masterkey"
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file("keystore.jks")
            storePassword keystoreProperties['storePassword']
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            signingConfig signingConfigs.config
        }
        demo {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "Boolean", "DEBUG_MODE", "false"
            signingConfig signingConfigs.config
            versionNameSuffix "-demo"
            applicationIdSuffix ".demo"
        }
        debug {
            buildConfigField "Boolean", "DEBUG_MODE", "true"
            signingConfig signingConfigs.config
            versionNameSuffix  "-debug"
            applicationIdSuffix ".debug"
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.google.android.gms:play-services-auth:11.0.4'
    compile 'org.projectlombok:lombok:1.16.18'
    testCompile 'junit:junit:4.12'
}
